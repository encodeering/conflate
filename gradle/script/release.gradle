subprojects {
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'

    publishing {
        publications {
            maven (MavenPublication) {
                pom.withXml {
                    asNode ().appendNode ("description", "$project.description")
                }
            }
        }
    }

    bintray {
        user = System.getenv ('BINTRAY_USER')
        key  = System.getenv ('BINTRAY_UKEY')
        publications = ['maven']
        pkg {
            name = rootProject.name
            version {
                name     = project.version
                vcsTag   = project.version
                released = new Date()
            }
        }
    }

    ifJava {
        publishing {
            publications {
                maven (MavenPublication) {
                    artifactId jar.baseName
                    from components.java
                }
            }
        }
    }

    ifAndroid {
        task jaronly (type: Jar, dependsOn : [build]) {
            from "$buildDir/intermediates/classes/release"
            archiveName "${project.name}.jar"
        }

        publishing {
            publications {
                maven (MavenPublication) {
                    artifact jaronly

                    pom.withXml {
                        def dependencies = asNode ().appendNode ('dependencies')

                        configurations.compile.allDependencies.each {
                            def dependency = dependencies.appendNode ('dependency')
                                dependency.appendNode ('groupId', it.group)
                                dependency.appendNode ('artifactId', it.name)
                                dependency.appendNode ('version', it.version)
                        }
                    }
                }
            }
        }
    }

    ifDokka {
        task dokkajar (type: Jar, dependsOn: dokka) {
            classifier = 'javadoc'
            from dokka.outputDirectory
        }

        publishing {
            publications {
                maven (MavenPublication) {
                    artifact dokkajar
                }
            }
        }
    }
}
