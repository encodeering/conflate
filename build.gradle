apply plugin: 'idea'

buildscript {
    ext.kotlinversion = '1.1.2'
    ext.junitversion  = '1.0.0-M2'

    repositories {
        jcenter ()
        mavenCentral ()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinversion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitversion"
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'org.junit.platform.gradle.plugin'

    group 'com.encodeering.conflate'
    version '0.0.1-SNAPSHOT'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    repositories {
        jcenter ()
        mavenCentral ()
        mavenLocal ()
    }

    dependencies {
        compile     "org.slf4j:slf4j-api:1.7.22"
        compile     "org.jetbrains.kotlin:kotlin-reflect:$kotlinversion"
    }

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
        project {
            languageLevel = 1.6
        }
    }

    publishing {
        publications {
            maven (MavenPublication) {
                artifactId jar.baseName
                from components.java
            }

            maven (MavenPublication) {
                pom.withXml {
                    asNode ().appendNode ("description", "$project.description")
                }
            }
        }
    }

    plugins.withType (org.junit.platform.gradle.plugin.JUnitPlatformPlugin) {
        dependencies {
            testCompile "org.junit.platform:junit-platform-runner:$junitversion"
            testCompile "com.winterbe:expekt:0.5.0"
            testCompile "org.jetbrains.spek:spek-api:1.0.89"
            testRuntime "org.jetbrains.spek:spek-junit-platform-engine:1.0.89"
            testCompile "org.mockito:mockito-core:2.7.5"
        }

        junitPlatform {
            platformVersion "$junitversion"
            engines {
                include 'spek'
            }
        }
    }

    plugins.withType (org.jetbrains.kotlin.gradle.plugin.KotlinPlugin) {
        sourceSets {
            main.kotlin.srcDirs += 'src/main/kotlin'
            test.kotlin.srcDirs += 'src/test/kotlin'
        }

        compileKotlin {
            kotlinOptions {
                apiVersion = '1.1'
                languageVersion = '1.1'
            }
        }
    }
}

task wrapper (type: Wrapper) {
    gradleVersion = '3.1'
}